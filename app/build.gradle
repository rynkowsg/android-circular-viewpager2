import twobuffers.buildsrc.Libs
import twobuffers.buildsrc.Versions

ext {
    ci = System.getenv("CI") == "true"
}

apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'
kapt {
    correctErrorTypes = true
    useBuildCache = true
}

apply plugin: "androidx.navigation.safeargs.kotlin"

apply plugin: 'com.getkeepsafe.dexcount'
dexcount {
    enabled = ci
}

android {
    compileSdkVersion buildConfig.compileSdk
    buildToolsVersion buildConfig.buildTools

    defaultConfig {
        applicationId "com.twobuffers.circularviewpager2"
        minSdkVersion buildConfig.minSdk
        targetSdkVersion buildConfig.targetSdk
        versionCode 1
        versionName "1.0"
        multiDexEnabled = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility = Versions.JAVA_VERSION_STR
        targetCompatibility = Versions.JAVA_VERSION_STR
    }

    dexOptions {
        // Don't pre-dex on CI
        preDexLibraries !ci
    }

    lintOptions {
        // Disable lintVital. Not needed since lint is run on CI
        checkReleaseBuilds false
        // Allow lint to check dependencies
        checkDependencies true
        // Ignore any tests
        ignoreTestSources true
        // Config file
        lintConfig project.file('lint-config.xml')
    }

    dataBinding.enabled = true
    viewBinding.enabled = true
}

dependencies {
    implementation Libs.Kotlin.stdlib
    implementation Libs.Google.material
}
